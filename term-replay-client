#!/usr/bin/perl -w

use strict;
use IO::Pty::Easy;
use Time::HiRes;

use bytes;

$|=1;

die "Usage: $0 LOGFILE COMMAND...\n" unless (scalar @ARGV > 1);

my $logfile = shift;

my $command = join " ", @ARGV;

my $pty = new IO::Pty::Easy;

$pty->spawn( $command );

if ( fork == 0 ) {
  while ( $pty->is_active ) {
    my $output = $pty->read;
    print $output;
  }
  exit;
}

#sleep 10;

open LOGFILE, $logfile or die "$!";

my $last_timestamp = 0;

my $last_tag = "";

while ( <LOGFILE> ) {
  chomp;
  my ( $timestamp, $tag ) = m{^(\d+) (\w+)};

  my $elapsed_time = $timestamp - $last_timestamp;
  die unless $elapsed_time >= 0;

  if ( $tag eq "HOST" ) {
    if ( $last_tag eq "HOST" ) {
      if ( $elapsed_time >= 150_000 ) {
	print STDERR "Shortening anticipated subsequent host delay to 0.15s\n";
	$elapsed_time = 150_000;
      }
    } elsif ( $elapsed_time >= 5_500_000 ) {
      printf STDERR "Shortening anticipated host delay to 5.5s (from %.1f seconds)\n",
	$elapsed_time / 1_000_000.0;
      $elapsed_time = 5_500_000;
    }
  } elsif ( $tag eq "USER" ) {
    if ( $elapsed_time >= 5_000_000 ) {
      printf STDERR "Shortening user delay to 5s (from %.1f seconds)\n",
	$elapsed_time / 1_000_000.0;
      $elapsed_time = 5_000_000;
    }
  }

  if ( $last_timestamp != 0 ) {
    Time::HiRes::usleep( $elapsed_time );
  }

  $last_timestamp = $timestamp;
  $last_tag = $tag;

  print STDERR "$tag after $elapsed_time us\n";

  if ( $tag eq "SIZE" ) {
    my ( $width, $height ) = m{SIZE (\d+) (\d+)};
    print STDERR "New size: $width x $height\n";
  } else {
    my ( $len ) = m{\Q$tag\E (\d+)};
    my $message;
    read LOGFILE, $message, $len;
    my $nl;
    read LOGFILE, $nl, 1;
    die unless $nl eq "\n";

    if ( $tag eq "USER" ) {
      $pty->write( $message ) or die;
      print (chr 20);
    }
  }
}
